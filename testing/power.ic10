# This script displays avearage and peak power from wind and solar power sources.
# It is only for gathering data, not for any useful in-game purpose.

alias averageTallDisplay d0
alias averageShortDisplay d1
alias averageSolarDisplay d2
alias peakTallDisplay d3
alias peakShortDisplay d4
alias peakSolarDisplay d5

alias totalTall r15
alias totalShort r14
alias totalSolar r13
alias counter r12
alias peakTall r11
alias peakShort r10
alias peakSolar r9
alias currState r8

move totalTall 0
move totalShort 0
move totalSolar 0
move counter 0
move peakTall 0
move peakShort 0
move peakSolar 0
move currState 0
move r5 0

define tall -2082355173
define short 1622183451
define solar -934345724
define weather 1997212478
define daysense 1076425094

s averageShortDisplay Mode 2
s averageTallDisplay Mode 2
s averageSolarDisplay Mode 2
s peakTallDisplay Mode 2
s peakShortDisplay Mode 2
s peakSolarDisplay Mode 2

startPoint:
yield
lb r6 daysense SolarAngle Maximum
bgtal r6 r5 updatePoint
sgt r4 r6 r5
s db Setting r4
blt r6 r5 start
j startPoint

updatePoint:
move r5 r6
j ra

start:
yield
lb currState weather Mode Maximum
blt currState 2 start # Comment this out to track normal weather
beq currState 2 start # Comment this out to track storms
lb r0 tall PowerGeneration Maximum
add totalTall totalTall r0
bgtal r0 peakTall updatePeakTall
lb r0 short PowerGeneration Maximum
add totalShort totalShort r0
bgtal r0 peakShort updatePeakShort
lb r0 solar Charge Maximum
add totalSolar totalSolar r0
bgtal r0 peakSolar updatePeakSolar
add counter counter 1
s db Setting counter
lb r6 daysense SolarAngle Maximum
bgtal r6 r5 updateDisplays
move r5 r6
j start

updatePeakTall:
move peakTall r0
j ra

updatePeakShort:
move peakShort r0
j ra

updatePeakSolar:
move peakSolar r0
j ra

updateDisplays:
div r7 totalTall counter
s averageTallDisplay Setting r7
div r7 totalShort counter
s averageShortDisplay Setting r7
div r7 totalSolar counter
s averageSolarDisplay Setting r7
s peakTallDisplay Setting peakTall
s peakShortDisplay Setting peakShort
s peakSolarDisplay Setting peakSolar
j ra